plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.devtools.ksp'
    id "io.sentry.android.gradle" version "4.5.0"
}


android {
    compileSdk 34
    Properties properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())

    configurations {
        configureEach {
            exclude module: 'commons-logging'
            exclude module: 'httpclient'
        }
    }

    defaultConfig {
        applicationId "com.pg.freememory"
        minSdk 21
        targetSdk 34
        versionCode 5
        versionName "1.0.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            ndk {
                debugSymbolLevel 'symbol_table'
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.getMajorVersion()
    }

    buildFeatures {
        compose true
        buildConfig true
    }
    ksp {
        arg("room.schemaLocation", "$projectDir/schemas")
    }
    // Added Extra Meta-Inf as caches is unable to discrete from the google auth libraries
    // for ref : https://github.com/auth0/Auth0.Android/issues/598
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE.txt', 'META-INF/LICENSE',
                         'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/notice.txt',
                         'META-INF/*.kotlin_module', '/META-INF/{AL2.0,LGPL2.1}',
                         'META-INF/gradle/incremental.annotation.processors', 'META-INF/*']
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.12"
    }
    namespace 'com.pg.cloudcleaner'
}

dependencies {

    // Core android Arch and Support Libraries
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.7.0'
    implementation 'androidx.core:core-ktx:1.13.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'

    // Room DB for Offline Storage
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    // Compose and UI level Libraries
    implementation platform('androidx.compose:compose-bom:2024.04.01')
    implementation 'androidx.compose.ui:ui'
    implementation "androidx.compose.foundation:foundation"
    implementation 'androidx.activity:activity-compose'
    implementation "androidx.compose.runtime:runtime"
    implementation "androidx.compose.material:material-icons-extended"
    debugImplementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.navigation:navigation-compose:2.7.7"
    implementation 'androidx.compose.runtime:runtime-livedata'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.accompanist:accompanist-swiperefresh:0.34.0'

    // De-sugaring Library
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.0.4")

    // for networking
    implementation 'com.squareup.retrofit2:retrofit:2.10.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    // PlayStore Dependencies
    implementation 'com.google.android.gms:play-services-auth:21.1.0'
    implementation 'com.google.api-client:google-api-client:2.4.0'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.35.0'
    implementation 'com.google.apis:google-api-services-drive:v3-rev20220815-2.0.0'

    // WorkManager for Background and Recurring Task
    def work_version = "2.9.0"
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation("androidx.concurrent:concurrent-futures-ktx:1.1.0")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.0")

    // Image loading library 1
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    // Glide v4 uses this new annotation processor -- see https://bumptech.github.io/glide/doc/generatedapi.html
    ksp 'com.github.bumptech.glide:compiler:4.16.0'
    implementation 'com.github.bumptech.glide:compose:1.0.0-beta01'
    // Image loading library 2
    implementation("io.coil-kt:coil-compose:2.6.0")
    implementation('io.coil-kt:coil-video:2.6.0')

    // For Video Playback
    def exo_version = '2.19.1'
    implementation "com.google.android.exoplayer:exoplayer-core:${exo_version}"
    implementation "com.google.android.exoplayer:exoplayer-dash:${exo_version}"
    implementation "com.google.android.exoplayer:exoplayer-ui:${exo_version}"

    // Logging
    implementation 'com.jakewharton.timber:timber:5.0.1'
    // sentry installation for crash Logging
    implementation 'io.sentry:sentry-android:7.8.0'
    implementation 'io.sentry:sentry-compose-android:7.8.0'

    // For testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

}
// Last Edited by : @Rohan on -> 2024.04.26